&НаКлиенте
Асинх Функция ВыполнитьРегистрацию()
	
	Ответ = Ждать ВопросАсинх(НСтр("ru = 'Система взаимодействие не зарегистрирована. Выполнить регистрацию?'", "ru"),
							  РежимДиалогаВопрос.ДаНет);
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ОткрытьФорму("Обработка.УправлениеСистемойВзаимодействия.Форма.Форма");
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура ЗапуститьОбработкуБотов()
	СистемаВзаимодействия.ВыполнитьОбработкуБотов();	
КонецПроцедуры	

&НаСервере
Функция ПолучитьНавигационнуюСсылкуНаОбсуждение()
	
	ИдентификаторПользователяСистемыВзаимодействияОфисМенеджер = БотСервер.ПолучитьИдентификаторБота();
	
	ИдентификаторОбсуждения = БотСервер.ПолучитьИдентификаторОбсуждения(
								СистемаВзаимодействия.ИдентификаторТекущегоПользователя(),
								ИдентификаторПользователяСистемыВзаимодействияОфисМенеджер);
								
	Возврат ПолучитьНавигационнуюСсылку(ИдентификаторОбсуждения);
	
КонецФункции

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	Если НЕ СистемаВзаимодействия.ИнформационнаяБазаЗарегистрирована() Тогда
		ВыполнитьРегистрацию();
		Возврат;
	КонецЕсли;
	
	ЗапуститьОбработкуБотов();
	
	НавигационнаяСсылка = ПолучитьНавигационнуюСсылкуНаОбсуждение();
	ПерейтиПоНавигационнойСсылке(НавигационнаяСсылка);
	
КонецПроцедуры
