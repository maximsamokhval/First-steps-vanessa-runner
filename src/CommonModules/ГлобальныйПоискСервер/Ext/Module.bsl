Процедура ГлобальныйПоискПоКоду(СтрокаПоиска, РезультатПоиска, ДопПараметры) Экспорт
	
	Номер = СтрокаПоиска;
	Пока СтрДлина(Номер) < 9 Цикл
		Номер = "0" + Номер;
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 	"ВЫБРАТЬ
	|	Заказ.Ссылка КАК Ссылка,
	|	Заказ.Представление КАК Представление
	|ИЗ
	|	Документ.Заказ КАК Заказ
	|ГДЕ
	|	Заказ.Номер = &Номер
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПриходТовара.Ссылка,
	|	ПриходТовара.Представление
	|ИЗ
	|	Документ.ПриходТовара КАК ПриходТовара
	|ГДЕ
	|	ПриходТовара.Номер = &Номер
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РасходТовара.Ссылка,
	|	РасходТовара.Представление
	|ИЗ
	|	Документ.РасходТовара КАК РасходТовара
	|ГДЕ
	|	РасходТовара.Номер = &Номер
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Оплата.Ссылка,
	|	Оплата.Представление
	|ИЗ
	|	Документ.Оплата КАК Оплата
	|ГДЕ
	|	Оплата.Номер = &Номер";
	
	Запрос.УстановитьПараметр("Номер", Номер);
		
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		РезультатПоиска.Добавить(
			Выборка.Ссылка, 
			СтрНайтиИВыделитьОформлением(Выборка.Представление, Номер), 
			БиблиотекаКартинок.Документ);
	КонецЦикла;
	
КонецПроцедуры

Процедура ГлобальныйПоискПоказатьДолги(СтрокаПоиска, РезультатПоиска, ДопПараметры) Экспорт
	
	ИспользоватьОтборПоКонтрагентам = Ложь;
	ОтборПоКонтрагентам = Новый Массив;
	
	СтрокаДолг = "долг ";
	Если СтрДлина(СтрокаПоиска) > СтрДлина(СтрокаДолг) И Лев(НРег(СтрокаПоиска), СтрДлина(СтрокаДолг)) = СтрокаДолг Тогда
		// Выделяем часть строки после слова "долг"
		// Считаем, что это часть наименования контрагента
		Стр = Сред(СтрокаПоиска, 6);
		Стр = СокрЛП(Стр);
		Если ПустаяСтрока(Стр) Тогда
			Возврат;
		КонецЕсли;
		
		// Используем полнотекстовый поиск для получения списка контрагентов,
		// в наименовании которых искомая строка находится в начале слова
		ИспользоватьОтборПоКонтрагентам = Истина;
			
		СписокПолнотекстовогоПоиска = ПолнотекстовыйПоиск.СоздатьСписок(Стр + "*", 10);
		СписокПолнотекстовогоПоиска.ИспользованиеМетаданных = ИспользованиеМетаданныхПолнотекстовогоПоиска.НеИспользовать;
		СписокПолнотекстовогоПоиска.ПолучатьОписание = Ложь;
		СписокПолнотекстовогоПоиска.ПолучатьПредставление = Истина;
		
		// Ищем только среди контрагентов
		МассивОтбор = Новый Массив;
		МассивОтбор.Добавить(Метаданные.Справочники.Контрагенты);
		СписокПолнотекстовогоПоиска.ОбластьПоиска = МассивОтбор;
		
		СписокПолнотекстовогоПоиска.ПерваяЧасть();
		
		СтрНРег = НРег(Стр);
		Для А = 0 По СписокПолнотекстовогоПоиска.Количество() - 1 Цикл
			ЭлементСпискаПолнотекстовогоПоиска = СписокПолнотекстовогоПоиска.Получить(А);
			
			// Проверяем, что искомая строка находится в представлении
			Если СтрНайти(НРег(ЭлементСпискаПолнотекстовогоПоиска.Представление), СтрНРег) > 0 Тогда
				ОтборПоКонтрагентам.Добавить(ЭлементСпискаПолнотекстовогоПоиска.Значение);
				Если ОтборПоКонтрагентам.Количество() = 5 Тогда
					Прервать;
				КонецЕсли;
			КонецЕсли;

		КонецЦикла
			
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 5
		|	ВзаиморасчетыОстатки.Валюта КАК ВалютаСсылка,
		|	ВзаиморасчетыОстатки.Валюта.Представление КАК ВалютаПредставление,
		|	ВзаиморасчетыОстатки.СуммаОстаток КАК СуммаОстаток,
		|	ВзаиморасчетыОстатки.Контрагент КАК КонтрагентСсылка,
		|	ВзаиморасчетыОстатки.Контрагент.Представление КАК КонтрагентПредставление
		|ИЗ
		|	РегистрНакопления.Взаиморасчеты.Остатки КАК ВзаиморасчетыОстатки";
	
	Если ИспользоватьОтборПоКонтрагентам Тогда
		Запрос.Текст = Запрос.Текст + "
		|ГДЕ
		|	ВзаиморасчетыОстатки.Контрагент В(&Контрагенты)";
	КонецЕсли;
	
	Запрос.Текст = Запрос.Текст + "
		|УПОРЯДОЧИТЬ ПО
		|	СуммаОстаток УБЫВ";
	
	Если ИспользоватьОтборПоКонтрагентам Тогда
		Запрос.УстановитьПараметр("Контрагенты", ОтборПоКонтрагентам);
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		МассивСтрок = Новый Массив;
		МассивСтрок.Добавить(Выборка.КонтрагентПредставление);
		МассивСтрок.Добавить(" ");
		МассивСтрок.Добавить(Новый ФорматированнаяСтрока(
			Формат(Выборка.СуммаОстаток, "ЧДЦ=2"), 
			Новый Шрифт(,, Истина), 
			ЦветаСтиля.ЦветАкцента));
		
		Если ЗначениеЗаполнено(Выборка.ВалютаСсылка) Тогда
			МассивСтрок.Добавить(" ");
			МассивСтрок.Добавить(Выборка.ВалютаПредставление);
		КонецЕсли;
		
		Значение = Новый Структура;
		Значение.Вставить("Операция", "Взаиморасчеты");
		Значение.Вставить("Контрагент", Выборка.КонтрагентСсылка);
		Значение.Вставить("Валюта", Выборка.ВалютаСсылка);
		РезультатПоиска.Добавить(Значение, Новый ФорматированнаяСтрока(МассивСтрок), БиблиотекаКартинок.РегистрСведений);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ГлобальныйПоискПоискКонтрагента(СтрокаПоиска, РезультатПоиска, ДопПараметры) Экспорт
	
	Стр = СокрЛП(СтрокаПоиска);
	Строки = СтрРазделить(Стр, " ", Ложь);
	Стр = СтрСоединить(Строки, "* ") + "*";
	
	СписокПолнотекстовогоПоиска = ПолнотекстовыйПоиск.СоздатьСписок(Стр, 10);
	СписокПолнотекстовогоПоиска.ИспользованиеМетаданных = ИспользованиеМетаданныхПолнотекстовогоПоиска.НеИспользовать;
	СписокПолнотекстовогоПоиска.ПолучатьОписание = Ложь;
	СписокПолнотекстовогоПоиска.ПолучатьПредставление = Истина;
		
	// Ищем только среди контрагентов
	МассивОтбор = Новый Массив;
	МассивОтбор.Добавить(Метаданные.Справочники.Контрагенты);
	СписокПолнотекстовогоПоиска.ОбластьПоиска = МассивОтбор;
		
	СписокПолнотекстовогоПоиска.ПерваяЧасть();
	
	Для А = 0 По СписокПолнотекстовогоПоиска.Количество() - 1 Цикл
		ЭлементСпискаПолнотекстовогоПоиска = СписокПолнотекстовогоПоиска.Получить(А);
			
		Представление = СтрНайтиИВыделитьОформлением(ЭлементСпискаПолнотекстовогоПоиска.Представление, Строки);
		Если Представление <> Неопределено Тогда
			
			ЭлементРезультатаПоиска = Новый ЭлементРезультатаГлобальногоПоиска();
			ЭлементРезультатаПоиска.Значение = ЭлементСпискаПолнотекстовогоПоиска.Значение;
			ЭлементРезультатаПоиска.Представление = Представление;
			ЭлементРезультатаПоиска.Картинка = БиблиотекаКартинок.Контрагент;
			ЭлементРезультатаПоиска.Действия.Добавить("Заказ", НСтр("ru = 'Новый заказ'", "ru"), БиблиотекаКартинок.СоздатьЭлементСписка);
			
			РезультатПоиска.Добавить(ЭлементРезультатаПоиска);
			
			Если РезультатПоиска.Количество() = 5 Тогда
				Прервать;
			КонецЕсли;
			
		КонецЕсли;

	КонецЦикла;
	
КонецПроцедуры
	